@top Program { expression }

@skip { space | LineComment }
@precedence {
  member @left
  application @right
  add-unary
}

expression {
  Identifier |
  String |
  Number |
  Boolean |
  Null |
  Undefined |
  Object[@dynamicPrecedence=1] { "{" ~brace commaSep<ObjectProperty { Identifier ":" expression }> "}" } |
  Array { "[" commaSep<expression> "]" } |
  Tuple { "{" ~brace commaSep<expression> "}" } |
  Property { expression !member "." Identifier } |
  Element { expression !member "@" expression } |
  Application { expression !application expression } |
  Add { !add-unary ("+" | "-") expression } |
  Parentheses { "(" expression ")" }
}

@tokens {
  Identifier { $[a-zA-Z_]$[a-zA-Z_0-9]* }
  String { '"' (!["\\] | "\\" _)* '"' }
  Number { $[0-9]+ }
  Boolean { "#true" | "#false" }
  Null { "#null" }
  Undefined { "#undefined" }
  LineComment { "//" ![\n]* }
  space { $[ \t\n\r]+ }
  "(" ")"
  "[" "]"
  "{" "}"
  "," ":"
  "." "@"
  "+" "-"
}

commaSep<content> { "" | content ("," content)* }

@detectDelim
